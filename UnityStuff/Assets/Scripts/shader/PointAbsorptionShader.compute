#pragma kernel getIndicatorMask
#pragma kernel g1_dists
#pragma kernel Absorptions

float r_cell;       // outer radius
float r_sample;     // inner radius
float r_cell_sq;    // outer radius squared
float r_sample_sq;  // inner radius squared

float cos;          // cos to use for rotation matrix
float sin;          // sin to use for rotation matrix
float2 mu;          // absorption factors in cell, sample.

/* CONTENT: Read-only buffers. */
StructuredBuffer<float2> segment;                   // list of segment coordinates.

/* CONTENT: g1_dists from script, precomputed in shader. */
RWStructuredBuffer<float2> distancesOuter;          // Diffraction only in cell.
RWStructuredBuffer<float2> distancesInner;          // Diffraction only in sample.

/* Absorption */
RWStructuredBuffer<float3> absorptions;           // Pointwise absorption factors.
RWStructuredBuffer<uint3> indicatorMask;          // Pointwise indicatorCount if ray is diffracted in point.

float getIntersectX(float2 v, float radius_sq, bool right)
{
    if (right)
    { 
        return sqrt(abs(radius_sq - pow(v.y, 2)));
    } 
    return -sqrt(abs(radius_sq - pow(v.y, 2)));
}


float2 ER_dists_F1(float2 v)
{
    float d_cell = 0.0;
    float d_sample = 0.0;
    
    if (length(v) <= r_sample)
    {
        d_cell = abs(getIntersectX(v, r_sample_sq, false) - getIntersectX(v, r_cell_sq, false));
        d_sample = abs(v.x - getIntersectX(v, r_sample_sq, false));
    }
    return float2(d_cell, d_sample);
}


float2 ER_dists_F2(float2 v)
{
    float d_cell = 0.0;
    float d_sample = 0.0;
    if (length(v) > r_sample & length(v) <= r_cell)
    {
        float RENx_o = getIntersectX(v, r_cell_sq, false);
        if (v.x >= 0 & abs(v.y) < r_sample)
        {
            float RENx_i = getIntersectX(v, r_sample_sq, false);
            float REXx_i = getIntersectX(v, r_sample_sq, true);
            d_cell = abs(RENx_o - RENx_i) + abs(REXx_i - v.x);
            d_sample = abs(RENx_i - REXx_i);
        }
        else
        {
            d_cell = abs(RENx_o - v.x);
        }
    }
    return float2(d_cell, d_sample);
}


/* call ER_dists for coordinate per diffraction case, save in shader variable. */
[numthreads(1024,1,1)]
void g1_dists(uint3 id : SV_DispatchThreadID)
{
    distancesOuter[id.x] = ER_dists_F2(segment[id.x]); 
    distancesInner[id.x] = ER_dists_F1(segment[id.x]);
}


/* call ER_dists for coordinate per diffraction case, save in shader variable. */
[numthreads(1024,1,1)]
void getIndicatorMask(uint3 id : SV_DispatchThreadID)
{
    float norm = length(segment[id.x]);
    
    if (norm <= r_cell)
    {
        if (norm > r_sample)
        {
            // case A_{c,sc}.
            indicatorMask[id.x].y = true;   
            
            // case A_{c,c}.
            indicatorMask[id.x].z = true;   
        }
        else 
        {
            // case A_{c,c}.
            indicatorMask[id.x].x = true;   
        }
    }
}


[numthreads(1024,1,1)]
void Absorptions(uint3 id : SV_DispatchThreadID){
    //float2 rv = rotatePoint(id.x);//
    float2 rv = float2(
        cos*segment[id.x].x-sin*segment[id.x].y, 
        sin*segment[id.x].x+cos*segment[id.x].y
    );
    float norm = length(segment[id.x]);
    
    if (norm <= r_cell)
    {
        if (norm > r_sample)
        {
            float2 d_outer = distancesOuter[id.x] + ER_dists_F2(rv);
            absorptions[id.x].y = exp(-dot(mu, d_outer));                 // case A_{c,sc}.
            absorptions[id.x].z = exp(-dot(float2(mu.x, 0.0), d_outer));  // case A_{c,c}.
        }
        else 
        {
            absorptions[id.x].x = exp(-dot(mu, distancesInner[id.x] + ER_dists_F1(rv)));    // case A_{s,sc}.
        }
    }
}